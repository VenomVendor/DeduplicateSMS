apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId 'com.venomvendor.sms.deduplicate'
        targetSdkVersion 28
        versionName '0.0.0'
        versionCode 1
    }

    flavorDimensions 'buildType'
    productFlavors {
        froyo {
            dimension 'buildType'
            minSdkVersion 8
        }
        donut {
            dimension 'buildType'
            minSdkVersion 4
            maxSdkVersion 7
            targetSdkVersion 7
        }
    }

    signingConfigs {
        release {
            try {
                storeFile file(rootProject.ext.KEY_LOCATION)
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
                storePassword KEYSTORE_PASSWORD
            }
            catch (ex) {
                //noinspection GrUnresolvedAccess
                throw new InvalidUserDataException("You should define KEY_ALIAS, KEY_PASSWORD & " +
                        "KEYSTORE_PASSWORD in gradle.properties. \n ${ex.getLocalizedMessage()}")
            }
        }
    }

    buildTypes {
        release {
            debuggable !rootProject.ext.DEBUG
            jniDebuggable !rootProject.ext.DEBUG
            renderscriptDebuggable !rootProject.ext.DEBUG
            pseudoLocalesEnabled !rootProject.ext.DEBUG
            shrinkResources rootProject.ext.DEBUG
            minifyEnabled rootProject.ext.DEBUG
            signingConfig signingConfigs.release
            versionNameSuffix rootProject.ext.RELEASE_SUFFIX
            proguardFiles './pro-guard/proguard-optimize.pro',
                    './pro-guard/proguard-rules-aggressive.pro',
                    './pro-guard/proguard-rules-app.pro',
                    './pro-guard/proguard-rules-log.pro'
        }
        debug {
            debuggable rootProject.ext.DEBUG
            jniDebuggable rootProject.ext.DEBUG
            renderscriptDebuggable rootProject.ext.DEBUG
            pseudoLocalesEnabled rootProject.ext.DEBUG
            shrinkResources !rootProject.ext.DEBUG
            minifyEnabled !rootProject.ext.DEBUG
            signingConfig signingConfigs.release
            versionNameSuffix rootProject.ext.DEBUG_SUFFIX
        }
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude '.readme'
    }

    lintOptions {
        checkReleaseBuilds rootProject.ext.checkReleaseBuilds
        abortOnError rootProject.ext.abortOnError
        disable 'IconMissingDensityFolder'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == 'release') {
            productFlavors.each { flavor ->
                def name = flavor.name
                variant.outputs.each { output ->
                    output.versionNameOverride = rootProject.ext.versionName
                    output.versionCodeOverride = name == 'froyo' ? rootProject.ext.froyo : rootProject.ext.donut
                }
            }
        }
    }
}

dependencies {
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.9.15'
}
